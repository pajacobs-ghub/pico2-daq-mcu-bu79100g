; mcp3301.pio
; Collect data from a single MCP3301 chip.
;
; Hints taken from the C/C++ SDK and
; https://github.com/raspberrypi/pico-examples/blob/master/pio/spi/spi.pio
; 
; Peter J. 2025-04-12 First cut
;          2025-04-14 Shift the RX Pin, so that we may have several.
;
; Pin assignments
; CSn (GPIO5) is side-set pin 0
; CLK (GPIO6) is side-set pin 1
; RX0 (GPIO7) is IN pin 0

.program mcp3301_read
.pio_version RP2350
.fifo txrx
; sysclk at 150MHz (6.667ns period)
; Want the PIO instruction cycle to be 200ns
.clock_div 30.0
.in 1 left
.side_set 2

.wrap_target
bitloop:
	nop             side 0x0 [1]  ; CLK low
	in pins, 1      side 0x2      ; read RX pin, set CLK high
	jmp x-- bitloop side 0x2      ; CLK stays high as we go for the next bit.
	
	; If we fall-through to here, all bits are now shifted in
	; so we push them into the RX FIFO.
	push            side 0x0      ; take CLK low
	nop             side 0x0 [1]  ; keep CSn low a while longer
	
public entry_point:
    ; We block here until the CPU writes a word to the TX FIFO.
    ; We don't actually use that word as data but as a release.
	pull block      side 0x1 [1]  ; Block with CSn high and CLK low
	; We count the bits down, from 15 to 0.
	set x, 15       side 0x0      ; take CSn low, keep CLK low
.wrap

% c-sdk {
static inline void mcp3301_read_program_init(PIO pio, uint sm, uint offset)
{
	pio_sm_config c = mcp3301_read_program_get_default_config(offset);
	// RX (DO from MCP3301 0) input on GPIO7
	sm_config_set_in_pin_base(&c, 7);
	pio_gpio_init(pio, 7);
	// SPI is synchronous, so bypass input synchroniser to reduce input delay.
    hw_set_bits(&pio->input_sync_bypass, 1u << 7);
    pio_sm_set_consecutive_pindirs(pio, sm, 7, 1, false); // RX input
    // CSn, CLK output on GPIO5 and GPIO6, respectively
	sm_config_set_sideset_pin_base(&c, 5);
	pio_gpio_init(pio, 5); // CSn
	pio_gpio_init(pio, 6); // CLK
	pio_sm_set_consecutive_pindirs(pio, sm, 5, 2, true);
	
	pio_sm_init(pio, sm, offset + mcp3301_read_offset_entry_point, &c);
	pio_sm_set_enabled(pio, sm, true);
}
%}
